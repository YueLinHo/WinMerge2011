<head>
<meta http-equiv='MSThemeCompatible' content='yes'>
<title>XML2CSV - Select Conversion Template</title>
<style>
*
{
	font-family: sans-serif;
	font-size: 10pt;
}
select
{
	width: 100%;
	height: 100%;
}
button
{
	width: 6em;
	height: 1.6em;
}
label
{
	height: 1.6em;
}
#buttons
{
	position: absolute;
	right: 0;
	bottom: 0;
}
body
{
	overflow: hidden;
	margin-bottom: 1.6em;
}
</style>
</head>
<body>
<select id='ConversionTemplates' size='2'>
<script type='text/vbs'>
Option Explicit

Const ShellFolder = "HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders\Personal"

Dim sh, fso, dir, file

Set sh = CreateObject("Wscript.Shell")
Set fso = CreateObject("Scripting.FileSystemObject")

dir = sh.RegRead(ShellFolder)

For Each file In fso.GetFolder(dir & "\XLS2CSV Profiles").Files
	document.writeln "<option value='" & file.ShortPath & "'>" & file.Name & "</option>"
Next

Sub OK_OnClick
	If UseAlways.checked Then
		fso.CopyFile ConversionTemplates.value, dialogArguments.ConversionTemplate, False
	Else
		dialogArguments.ConversionTemplate = ConversionTemplates.value
	End If
	dialogArguments.Confirmed = True
	close
End Sub

Sub Cancel_OnClick
	close
End Sub

Sub Window_OnLoad
	ConversionTemplates.selectedIndex = 0
End Sub

Sub Window_OnUnload
	If IsEmpty(dialogArguments.Confirmed) Then
		dialogArguments.Confirmed = False
	End If
End Sub

Sub ConversionTemplates_OnDblclick
	OK_OnClick
End Sub

Sub Document_OnKeypress
	Select Case window.event.keyCode
	Case 13
		OK_OnClick
		window.event.returnValue = False
	Case 27
		Cancel_OnClick
		window.event.returnValue = False
	End Select
End Sub

</script>
</select>
<label for='UseAlways'>
	<input id='UseAlways' type='checkbox'/>
	Always use this template
</label>
<div id='buttons'>
	<button id='OK'>OK</button>
	<button id='Cancel'>Cancel</button>
</div>
</body>
