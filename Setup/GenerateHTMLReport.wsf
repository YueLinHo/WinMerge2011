<package>
<job>
<script type='job' language='vbs'>
Option Explicit

Dim cmd, arg, sh, search

Set sh = CreateObject("WScript.Shell")
cmd = """" & sh.RegRead("HKLM\Software\Thingamahoochie\WinMerge\Executable") & """"

For Each arg In WScript.arguments
	If InStr(arg, "/out:") = 1 Then
		search = search & Replace(arg, "/out:", ":out=%22", 1, 1) & "%22"
	ElseIf InStr(arg, "/context:") = 1 Then
		search = search & Replace(arg, "/context:", ":context=", 1, 1)
	ElseIf InStr(arg, "/") = 1 Then
		cmd = cmd & " " & arg
	Else
		cmd = cmd & " """ & arg & """"
	End If
Next

If IsEmpty(search) Then
	cmd = "%SystemRoot%\System32\mshta.exe """ & WScript.ScriptFullName & """"
Else
	cmd = cmd & " /run ""modal:" & WScript.ScriptFullName & Replace(search, ":", "?", 1, 1) & """"
End If

sh.Run cmd, 1

</script>
</job>
<head>
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8'/>
</head>

<script language='vbs'>
Option Explicit

If Not external Is Nothing Then
	Dim out, context
	Execute Mid(location.search, 2)
	returnValue = False
	If Not IsEmpty(context) Then
		dialogArguments.LimitContext context
	End If
	dialogArguments.WriteReport out
	close
End If
</script>
<body>
<pre>
Usage:

GenerateHTMLReport.wsf	[options]
			"&lt;path to left file&gt;"
			"&lt;path to right file&gt;"
			/out:"&lt;path to report file&gt;"
			[/context:&lt;number of context lines&gt;]
</pre>
</body>
</package>
