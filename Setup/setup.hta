<head>
<meta http-equiv='Content-Type' content='text/html; charset=ISO-8859-1'/>
<hta:application
	id='app'
	version='XXXXX.2011.007.205'
	applicationName='WinMerge &amp;version; Setup'
	scroll='no'
	singleInstance='yes'
	icon='WinMergeU.exe'/>
<comment id='setup_cfg'>7z%BC%AF%27%1C
;!@Install@!UTF-8!
Title="WinMerge &version; [&platform;]"
BeginPrompt="Do you want to install WinMerge &version;?"
RunProgram="%%P:\"%SystemRoot%\\system32\\mshta.exe\" \"%%T\\setup.hta\" \"install\" \"&platform;\" \"ModuleAtom:4e716236-aa30-4c65-b225-d68bba81e9c2\""
;!@InstallEnd@!
</comment>
<comment id='setup_bat'>
call "%VS100COMNTOOLS%vsvars32.bat"
set SevenZip=&SevenZip;
del "WinMerge_&version;_&platform;.7z"
"%SevenZip%7z.exe" a "WinMerge_&version;_&platform;.7z" -r .\Build\WinMerge\&platform;\Release\* .\Build\WinMerge\Win32\Release\ShellExtensionU.dll .\Build\WinMerge\x64\Release\ShellExtensionX64.dll -x!*.lib -x!*.exp -x!*.pdb -x!*.json -x!*.zip -x!*.bak -x!xdoc2txt\xdoc2txt.*
copy /b "Setup\7zsd_LZMA2.sfx" "WinMerge_&version;_&platform;_change_7z_to_exe_if_you_want_a_setup.7z"
mt.exe -manifest "Setup\7zsd.sfx.manifest" -outputresource:"WinMerge_&version;_&platform;_change_7z_to_exe_if_you_want_a_setup.7z";1
copy /b "WinMerge_&version;_&platform;_change_7z_to_exe_if_you_want_a_setup.7z" + Setup_&platform;.cfg + "WinMerge_&version;_&platform;.7z" "WinMerge_&version;_&platform;_change_7z_to_exe_if_you_want_a_setup.7z"
</comment>
<comment id='version_h'>
#define MAJOR &major;
#define MINOR &minor;
#define &kind; &www;
#define PRIVATE_BUILD ""
#define COMMENTS ""
</comment>
<comment id='xdoc2txt_prompt'>
WinMerge comes with a file transform scriptlet for piping office-type files through xdoc2txt.exe.
Due to lack of a redistribution license, xdoc2txt.exe cannot legally be included in the WinMerge distribution.
However, Setup can assist you in downloading the xdoc2txt distribution package from the official download location,
and after successful sha256 verification, extract the relevant file(s) to their expected location(s).

Do you want that to happen?
</comment>

<style>
body
{
	font: 10pt verdana;
	margin: 10 10 16em 10;
	background-color: lightblue;
	overflow: hidden;
}
td label
{
	height: 1.3em;
}
#license
{
	display: inline;
	vertical-align: top;
	width: 100%;
}
#shortcuts
{
	position: absolute;
	right: 10;
	display: inline;
	width: 19em;
}
table
{
	height: 6em;
}
textarea
{
	width: 100%;
	height: 100%;
	margin-right: 19em;
}
caption, td
{
	white-space: nowrap;
	text-align: left;
	font: 10pt verdana;
}
#south input
{
	font: 10pt verdana;
}
button
{
	font: 10pt verdana;
	width: 5.5em;
	margin-right: 10;
}
#StFolder
{
	width: 0;
	padding-bottom: 2pt;
}
#EdFolder
{
	width: 100%;
	margin-left: 1em;
	margin-right: 36em;
}
#PbFolder
{
	position: absolute;
	right: 22em;
}
#media
{
	position: absolute;
	right: 13em;
	padding-bottom: 2pt;
}
#PbOK
{
	position: absolute;
	right: 6em;
}
#PbCancel
{
	position: absolute;
	right: 0;
}
#sheet
{
	height: 100%;
}
#north
{
	margin-bottom: 10;
}
#south
{
	white-space: nowrap;
	margin-top: 10;
}
#readme
{
	width: 100%;
	height: 100%;
	display: block;
}
a
{
	line-height: 1.5;
	color: windowtext;
	background-color: buttonface;
	text-decoration: none;
	white-space: nowrap;
	border: 2 outset;
	padding: 0 4 1000 4;
}
a:active
{
	color: highlighttext;
	background-color: buttonshadow;
}
a.selected, a:active.selected
{
	color: highlighttext;
	background-color: highlight;
}
</style>
<style id='maintenance'>
#StFolder, #EdFolder, #PbFolder, #media
{
	visibility: hidden;
}
</style>
</head>
<script type='text/javascript'>
/*
 * A JavaScript implementation of the Secure Hash Algorithm, SHA-256, as defined
 * in FIPS 180-2
 * Version 2.2 Copyright Angel Marin, Paul Johnston 2000 - 2009.
 * Other contributors: Greg Holt, Andrew Kepert, Ydnar, Lostinet
 * Distributed under the BSD License
 * See http://pajhome.org.uk/crypt/md5 for details.
 * Also http://anmar.eu.org/projects/jssha2/
 *
 * This is a modified version to support the specific task at hand.
 */

/*
 * This is the function to call from VBScript.
 * It takes a byte array argument and returns a hex encoded string.
 */
function hex_sha256(s) { return rstr2hex(rstr_sha256(new VBArray(s).toArray())); }

/*
 * Calculate the sha256 of a raw string
 */
function rstr_sha256(s)
{
  return binb2rstr(binb_sha256(rstr2binb(s), s.length * 8));
}

/*
 * Convert a raw string to a hex string
 */
function rstr2hex(input)
{
  var output = "";
  for(var i = 0; i < input.length; i++)
    output += "0".concat(input.charCodeAt(i).toString(16)).slice(-2);
  return output.toUpperCase();
}

/*
 * Convert a raw string to an array of big-endian words
 * Characters >255 have their high-byte silently ignored.
 */
function rstr2binb(input)
{
  var output = Array(input.length >> 2);
  for(var i = 0; i < output.length; i++)
    output[i] = 0;
  for(var i = 0; i < input.length * 8; i += 8)
    output[i>>5] |= (input[i / 8] & 0xFF) << (24 - i % 32);
  return output;
}

/*
 * Convert an array of big-endian words to a string
 */
function binb2rstr(input)
{
  var output = "";
  for(var i = 0; i < input.length * 32; i += 8)
    output += String.fromCharCode((input[i>>5] >>> (24 - i % 32)) & 0xFF);
  return output;
}

/*
 * Main sha256 function, with its support functions
 */
function sha256_S (X, n) {return ( X >>> n ) | (X << (32 - n));}
function sha256_R (X, n) {return ( X >>> n );}
function sha256_Ch(x, y, z) {return ((x & y) ^ ((~x) & z));}
function sha256_Maj(x, y, z) {return ((x & y) ^ (x & z) ^ (y & z));}
function sha256_Sigma0256(x) {return (sha256_S(x, 2) ^ sha256_S(x, 13) ^ sha256_S(x, 22));}
function sha256_Sigma1256(x) {return (sha256_S(x, 6) ^ sha256_S(x, 11) ^ sha256_S(x, 25));}
function sha256_Gamma0256(x) {return (sha256_S(x, 7) ^ sha256_S(x, 18) ^ sha256_R(x, 3));}
function sha256_Gamma1256(x) {return (sha256_S(x, 17) ^ sha256_S(x, 19) ^ sha256_R(x, 10));}
function sha256_Sigma0512(x) {return (sha256_S(x, 28) ^ sha256_S(x, 34) ^ sha256_S(x, 39));}
function sha256_Sigma1512(x) {return (sha256_S(x, 14) ^ sha256_S(x, 18) ^ sha256_S(x, 41));}
function sha256_Gamma0512(x) {return (sha256_S(x, 1)  ^ sha256_S(x, 8) ^ sha256_R(x, 7));}
function sha256_Gamma1512(x) {return (sha256_S(x, 19) ^ sha256_S(x, 61) ^ sha256_R(x, 6));}

var sha256_K = new Array
(
  1116352408, 1899447441, -1245643825, -373957723, 961987163, 1508970993,
  -1841331548, -1424204075, -670586216, 310598401, 607225278, 1426881987,
  1925078388, -2132889090, -1680079193, -1046744716, -459576895, -272742522,
  264347078, 604807628, 770255983, 1249150122, 1555081692, 1996064986,
  -1740746414, -1473132947, -1341970488, -1084653625, -958395405, -710438585,
  113926993, 338241895, 666307205, 773529912, 1294757372, 1396182291,
  1695183700, 1986661051, -2117940946, -1838011259, -1564481375, -1474664885,
  -1035236496, -949202525, -778901479, -694614492, -200395387, 275423344,
  430227734, 506948616, 659060556, 883997877, 958139571, 1322822218,
  1537002063, 1747873779, 1955562222, 2024104815, -2067236844, -1933114872,
  -1866530822, -1538233109, -1090935817, -965641998
);

function binb_sha256(m, l)
{
  var HASH = new Array(1779033703, -1150833019, 1013904242, -1521486534,
                       1359893119, -1694144372, 528734635, 1541459225);
  var W = new Array(64);
  var a, b, c, d, e, f, g, h;
  var i, j, T1, T2;

  /* append padding */
  m[l >> 5] |= 0x80 << (24 - l % 32);
  m[((l + 64 >> 9) << 4) + 15] = l;

  for(i = 0; i < m.length; i += 16)
  {
    a = HASH[0];
    b = HASH[1];
    c = HASH[2];
    d = HASH[3];
    e = HASH[4];
    f = HASH[5];
    g = HASH[6];
    h = HASH[7];

    for(j = 0; j < 64; j++)
    {
      if (j < 16) W[j] = m[j + i];
      else W[j] = safe_add(safe_add(safe_add(sha256_Gamma1256(W[j - 2]), W[j - 7]),
                                            sha256_Gamma0256(W[j - 15])), W[j - 16]);

      T1 = safe_add(safe_add(safe_add(safe_add(h, sha256_Sigma1256(e)), sha256_Ch(e, f, g)),
                                                          sha256_K[j]), W[j]);
      T2 = safe_add(sha256_Sigma0256(a), sha256_Maj(a, b, c));
      h = g;
      g = f;
      f = e;
      e = safe_add(d, T1);
      d = c;
      c = b;
      b = a;
      a = safe_add(T1, T2);
    }

    HASH[0] = safe_add(a, HASH[0]);
    HASH[1] = safe_add(b, HASH[1]);
    HASH[2] = safe_add(c, HASH[2]);
    HASH[3] = safe_add(d, HASH[3]);
    HASH[4] = safe_add(e, HASH[4]);
    HASH[5] = safe_add(f, HASH[5]);
    HASH[6] = safe_add(g, HASH[6]);
    HASH[7] = safe_add(h, HASH[7]);
  }
  return HASH;
}

function safe_add (x, y)
{
  var lsw = (x & 0xFFFF) + (y & 0xFFFF);
  var msw = (x >> 16) + (y >> 16) + (lsw >> 16);
  return (msw << 16) | (lsw & 0xFFFF);
}
</script>
<script type='text/vbs'>
Option Explicit

Const PROCESSOR_ARCHITECTURE_IA64 = 6
Const PROCESSOR_ARCHITECTURE_AMD64 = 9

Const adTypeBinary = 1
Const adSaveCreateOverWrite = 2

Const xdoc2txt_source = "http://ebstudio.info/download/KWICFinder/xd2tx2161.zip"
Const xdoc2txt_digest = "B8CE64177EBD63EC3EBE5CA8CD3740E69F295B33A97FF84CDD47735E01077DC3"

Dim sh, fso, sha, env, version, argv, executable
Dim sProgFolder, sThisFolder, sMoveFolder

Set sh = CreateObject("WScript.Shell")
Set fso = CreateObject("Scripting.FileSystemObject")
Set sha = CreateObject("Shell.Application")
Set env = sh.Environment("Process")

window.name = "4e716236-aa30-4c65-b225-d68bba81e9c2" & "-" & fso.GetTempName

On Error Resume Next
executable = sh.RegRead("HKLM\Software\Thingamahoochie\WinMerge\Executable")
On Error GoTo 0

Sub Job_Uninstall
	sProgFolder = fso.GetParentFolderName(app.icon)
	If MsgBox("Are you sure you want to uninstall WinMerge 2011 from folder" & vbLf & sProgFolder & "?", vbYesNo Or vbExclamation, document.title) = vbYes Then
		If TerminateWinMerge(document.title) Then
			DeleteLink "Desktop"
			DeleteLink "AllUsersDesktop"
			DeleteLink "StartMenu"
			DeleteLink "AllUsersStartMenu"
			DeleteLink "Programs"
			DeleteLink "AllUsersPrograms"
			DeleteLink "QuickLaunch"
			DeleteLink "ContextMenu"
			KillShellExtensionHosts
			On Error Resume Next
			sh.RegDelete "HKCR\.WinMerge\"
			sh.RegDelete "HKCR\WinMerge.Project.File\DefaultIcon\"
			sh.RegDelete "HKCR\WinMerge.Project.File\shell\edit\command\"
			sh.RegDelete "HKCR\WinMerge.Project.File\shell\edit\"
			sh.RegDelete "HKCR\WinMerge.Project.File\shell\open\command\"
			sh.RegDelete "HKCR\WinMerge.Project.File\shell\open\"
			sh.RegDelete "HKCR\WinMerge.Project.File\shell\"
			sh.RegDelete "HKCR\WinMerge.Project.File\"
			sh.RegDelete "HKLM\Software\Thingamahoochie\WinMerge\"
			' Unregister with ARP
			sh.RegDelete "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\"
			fso.DeleteFolder sProgFolder, True
			On Error GoTo 0
			MsgBox "Uninstall completed successfully", vbInformation Or vbSystemModal, document.title
		End If
	End If
	close
End Sub

version = app.version
version = Replace(version, "XXXXX.", "0.")
version = Replace(version, "BUILD.", "")
document.title = Replace(app.applicationName, "&version;", version)

window.onload = GetRef("Job_Default")
argv = Eval("Array(" & Replace(Trim(app.commandLine), """ ", """,") & ")")

If UBound(argv) > 0 Then
	Dim opt
	For Each opt In Split(argv(1), ":")
		Select Case LCase(opt)
		Case "hidden"
			moveTo 0, -16000
		Case Else
			window.onload = GetRef("Job_" & opt)
		End Select
	Next
End If

Function TerminateWinMerge(ScriptName)
	TerminateWinMerge = True
	If IsEmpty(executable) Then Exit Function
	Do
		On Error Resume Next
		fso.MoveFile executable, executable
		If Err.Number <> 70 Then Exit Function
		On Error GoTo 0
	Loop Until MsgBox("Please close your current WinMerge sessions, then click OK", vbExclamation Or vbOkCancel, ScriptName) = vbCancel
	TerminateWinMerge = False
End Function

Function Windows_NT
	On Error Resume Next
	Windows_NT = Eval(sh.RegRead("HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\CurrentVersion"))
	On Error GoTo 0
End Function

Function BuildLinkName(ByVal sPath)
	Select Case sPath
	Case "QuickLaunch"
		sPath = env("AppData") & "\Microsoft\Internet Explorer\Quick Launch"
	Case Else
		sPath = sh.SpecialFolders(sPath)
	End Select
	BuildLinkName = sPath & "\WinMerge.lnk"
End Function

Function LinkExists(ByVal sPath)
	If sPath = "ContextMenu" Then
		On Error Resume Next
		LinkExists = Not IsEmpty(sh.RegRead("HKCR\CLSID\{4E716236-AA30-4C65-B225-D68BBA81E9C2}\"))
		On Error GoTo 0
	Else
		sPath = BuildLinkName(sPath)
		LinkExists = fso.FileExists(sPath)
	End If
End Function

Sub KillShellExtensionHosts
	Select Case sha.GetSystemInformation("ProcessorArchitecture")
	Case PROCESSOR_ARCHITECTURE_IA64, PROCESSOR_ARCHITECTURE_AMD64
		sh.Run """RegSvr32.exe"" /s /n /i:" & window.name & " """ & sProgFolder & "\ShellExtensionX64.dll""", 1, True
	End Select
	sh.Run """RegSvr32.exe"" /s /n /i:" & window.name & " """ & sProgFolder & "\ShellExtensionU.dll""", 1, True
End Sub

Sub RegisterShellExtension(flags)
	Dim verb
	If Windows_NT >= 6.0 Then
		verb = "runas"
	Else
		verb = "open"
	End If
	Select Case sha.GetSystemInformation("ProcessorArchitecture")
	Case PROCESSOR_ARCHITECTURE_IA64, PROCESSOR_ARCHITECTURE_AMD64
		sha.ShellExecute "RegSvr32.exe", flags & """" & sProgFolder & "\ShellExtensionX64.dll""", "", verb
	End Select
	sha.ShellExecute "RegSvr32.exe", flags & """" & sProgFolder & "\ShellExtensionU.dll""", "", verb
End Sub

Sub WriteExecutablePath(root)
	sh.RegWrite root & "\Software\Thingamahoochie\WinMerge\Executable", sProgFolder & "\WinMergeU.exe"
	Select Case sha.GetSystemInformation("ProcessorArchitecture")
	Case PROCESSOR_ARCHITECTURE_IA64, PROCESSOR_ARCHITECTURE_AMD64
		sh.RegWrite root & "\Software\Wow6432Node\Thingamahoochie\WinMerge\Executable", sProgFolder & "\WinMergeU.exe"
	End Select
End Sub

Sub CreateLink(ByVal sPath)
	If sPath = "ContextMenu" Then
		RegisterShellExtension "/s "
	Else
		sPath = BuildLinkName(sPath)
		With sh.CreateShortcut(sPath)
			.TargetPath = sProgFolder & "\WinMergeU.exe"
			.WindowStyle = 1
			.Description = "WinMerge"
			.WorkingDirectory = sProgFolder
			.IconLocation = sProgFolder & "\WinMergeU.exe"
			.Save
		End With
	End If
End Sub

Sub DeleteLink(ByVal sPath)
	If sPath = "ContextMenu" Then
		RegisterShellExtension "/s /u "
	Else
		sPath = BuildLinkName(sPath)
		On Error Resume Next
		fso.DeleteFile sPath, True
		On Error GoTo 0
	End If
End Sub

Sub Job_Install
	Select Case sha.GetSystemInformation("ProcessorArchitecture")
	Case PROCESSOR_ARCHITECTURE_IA64, PROCESSOR_ARCHITECTURE_AMD64
	Case Else
		If argv(2) = "x64" Then
			MsgBox "This software is for 64 bit only. It cannot run on a 32 bit operating system.", vbError, document.title
			close
		End If
	End Select
	maintenance.disabled = True
	If IsEmpty(executable) Then
		EdFolder.value = env("ProgramFiles") & "\WinMerge2011"
	Else
		EdFolder.value = fso.GetParentFolderName(executable)
	End If
	Job_Default
End Sub

Function FormatText(s)
	FormatText = Trim(Replace(Replace(s, vbCrLf & vbCrLf, vbLf & vbLf), vbCrLf, " "))
End Function

Function TimeStampNow(length)
	Dim clock
	clock = Now
	TimeStampNow = Left(10000000000 * Year(clock) +_
		100000000 * Month(clock) + 1000000 * Day(clock) +_
		10000 * Hour(clock) + 100 * Minute(clock) + Second(clock), length)
End Function

Function InstallFiles
	If fso.FolderExists(sProgFolder) Then
		If Not TerminateWinMerge(document.title) Then Exit Function
		sMoveFolder = sProgFolder & ".RenamedBySetup-" & TimeStampNow(14)
		fso.MoveFolder sProgFolder, sMoveFolder
	End If
	fso.CreateFolder sProgFolder
	fso.CopyFolder sThisFolder, sProgFolder
	InstallFiles = True
End Function

Sub PbOK_OnClick
	document.body.style.cursor = "wait"
	document.body.setCapture
	setTimeout "Apply", 0, "VBS"
End Sub

Function DownloadFile(url)
	Dim req, stm
	Set req = CreateObject("MSXML2.XMLHTTP.3.0")
	req.open "GET", url, False
	req.send
	Set stm = Nothing
	If req.status = 200 Then
		Set stm = CreateObject("ADODB.Stream")
		stm.Type = adTypeBinary
		stm.Open
		stm.Write req.responseBody
		stm.Position = 0
	End If
	Set DownloadFile = stm
End Function

Function DownloadAndSave3rdPartyFile(source, digest, destination)
	Dim stm, sha256
	Set stm = DownloadFile(source)
	If stm Is Nothing Then
		MsgBox "3rd party distribution package download failed." & vbLf & vbLf &_
			"URL: " & xdoc2txt_source & vbLf & vbLf &_
			"Operation canceled.",_
			vbExclamation, document.title
		Exit Function
	End If
	sha256 = hex_sha256(stm.Read)
	If sha256 <> digest Then
		MsgBox "3rd party distribution package sha256 verification failed." & vbLf & vbLf &_
			"URL: " & xdoc2txt_source & vbLf & vbLf &_
			"Acquired hash:" & vbLf & sha256 & vbLf & vbLf &_
			"Expected hash:" & vbLf & digest & vbLf & vbLf &_
			"Operation canceled.",_
			vbExclamation, document.title
		Exit Function
	End If
	stm.SaveToFile destination, adSaveCreateOverWrite
	DownloadAndSave3rdPartyFile = True
End Function

Function Download3rdPartyFiles
	If MsgBox(FormatText(xdoc2txt_prompt.text), vbYesNo Or vbQuestion, document.title) = vbYes Then
		If Not DownloadAndSave3rdPartyFile(xdoc2txt_source, xdoc2txt_digest, sThisFolder & "\xdoc2txt.zip") Then Exit Function
	Else
		On Error Resume Next
		fso.DeleteFile sThisFolder & "\xdoc2txt.zip", True
		On Error Goto 0
	End If
	Download3rdPartyFiles = True
End Function

Function Install3rdPartyFiles
	If fso.FileExists(sThisFolder & "\xdoc2txt.zip") Then
		On Error Resume Next
		fso.CreateFolder sThisFolder & "\xdoc2txt"
		On Error Goto 0
		sha.NameSpace(sThisFolder & "\xdoc2txt").CopyHere(sha.NameSpace(sThisFolder & "\xdoc2txt.zip\command").Items)
		fso.DeleteFile sThisFolder & "\xdoc2txt.zip", True
	Else
		On Error Resume Next
		fso.DeleteFolder sThisFolder & "\xdoc2txt", True
		On Error Goto 0
	End If
	Install3rdPartyFiles = True
End Function

Sub Apply
	Dim element, sSetupCmdLine
	document.body.style.cursor = "arrow"
	sThisFolder = fso.GetParentFolderName(app.icon)
	If maintenance.disabled Then
		If Not Download3rdPartyFiles Then Exit Sub
		If Not Install3rdPartyFiles Then Exit Sub
		sProgFolder = EdFolder.value
		If Not InstallFiles Then Exit Sub
		' Preserve existing WinMergeU.json from former installation if present
		If Not IsEmpty(sMoveFolder) Then
			On Error Resume Next
			fso.CopyFile sMoveFolder & "\WinMergeU.json", sProgFolder & "\WinMergeU.json"
			On Error GoTo 0
		End If
	Else
		sProgFolder = sThisFolder
	End If
	If PortableMedia.checked Then
		fso.CreateTextFile sProgFolder & "\WinMergeU.json"
	Else
		For Each element In shortcuts.getElementsByTagName("INPUT")
			If element.defaultChecked Or maintenance.disabled Then
				If element.checked Then
					CreateLink element.id
				Else
					DeleteLink element.id
				End If
			End If
		Next
		' Write executable path to HKCU as well because TortoiseHg reads it from there
		WriteExecutablePath "HKCU"
		If maintenance.disabled Then
			sh.RegWrite "HKCR\.WinMerge\", "WinMerge.Project.File"
			sh.RegWrite "HKCR\WinMerge.Project.File\", "WinMerge Project File"
			sh.RegWrite "HKCR\WinMerge.Project.File\DefaultIcon\", """" & sProgFolder & "\WinMergeU.exe"",1"
			sh.RegWrite "HKCR\WinMerge.Project.File\shell\edit\command\", """NOTEPAD.EXE"" ""%1"""
			sh.RegWrite "HKCR\WinMerge.Project.File\shell\open\command\", """" & sProgFolder & "\WinMergeU.exe"" /run ""%1"" %*"
			WriteExecutablePath "HKLM"
			' Do the ARP stuff
			sSetupCmdLine = sh.ExpandEnvironmentStrings("""%SystemRoot%\system32\mshta.exe"" """ & sProgFolder & "\setup.hta""")
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\DisplayIcon", sProgFolder & "\WinMergeU.exe"
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\DisplayName", "WinMerge 2011"
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\DisplayVersion", version
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\InstallDate", TimeStampNow(8)
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\InstallLocation", sProgFolder
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\ModifyPath", sSetupCmdLine
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\Publisher", "Jochen Neubeck"
			sh.RegWrite "HKLM\Software\Microsoft\Windows\CurrentVersion\Uninstall\WinMerge 2011\UninstallString", sSetupCmdLine & " ""hidden:uninstall"" ""ModuleAtom:4e716236-aa30-4c65-b225-d68bba81e9c2"""
		End If
	End If
	If maintenance.disabled Then
		If IsEmpty(sMoveFolder) Then
			MsgBox "Setup completed successfully", vbInformation, document.title
		ElseIf MsgBox("Setup completed successfully." & vbLf & vbLf &_
				"The files from the previous installation have been moved to" & vbLf & vbLf &_
				sMoveFolder & vbLf & vbLf &_
				"Setup can try to delete this folder but may need to close" & vbLf &_
				"Windows Explorer and/or other applications, each of which" & vbLf &_
				"will trigger an extra confirmation prompt." & vbLf & vbLf &_
				"Do you want that to happen?" & vbLf & vbLf &_
				"!!>    CAUTION:" & vbLf &_
				"!!>    DO NOT CONFIRM THIS IF YOU HAVE MADE" & vbLf &_
				"!!>    CHANGES TO THE PREVIOUS INSTALLATION" & vbLf &_
				"!!>    AND WANT TO KEEP THEM FOR REFERENCE!",_
				vbYesNo Or vbExclamation, document.title) = vbYes Then
			KillShellExtensionHosts
			On Error Resume Next
			fso.DeleteFolder sMoveFolder, True
			On Error GoTo 0
		End If
	End If
	close
End Sub

Sub PbCancel_OnClick
	close
End Sub

Sub Job_Version
	Dim parts, content, path, platform
	parts = Split(app.version, ".")
	parts(3) = Right("0" & DatePart("ww", Now), 2) & DatePart("w", Now)
	parts(2) = Left(parts(2), 2) & (DatePart("yyyy", Now) - parts(1) + 1)
	content = version_h.text
	content = Replace(content, "&kind;", parts(0))
	content = Replace(content, "&major;", parts(1))
	content = Replace(content, "&minor;", parts(2))
	content = Replace(content, "&www;", parts(3))
	path = argv(2)
	If fso.OpenTextFile(path).ReadAll <> content Then
		fso.CreateTextFile(path, True).Write content
	End If
	platform = argv(3)
	version = Join(parts, ".")
	path = location.pathname
	content = Replace(fso.OpenTextFile(path).ReadAll, app.version, version)
	fso.CreateTextFile(path, True).Write content
	version = Replace(version, "XXXXX.", "0.")
	version = Replace(version, "BUILD.", "")
	content = Replace(setup_cfg.text, "&platform;", platform)
	content = Replace(content, "&version;", version)
	path = fso.GetParentFolderName(location.pathname) & "_" & platform & ".cfg"
	fso.CreateTextFile(path, True).Write Unescape(content)
	content = Replace(setup_bat.text, "&platform;", platform)
	content = Replace(content, "&version;_Win32", "&version;")
	content = Replace(content, "&version;", version)
	content = Replace(content, "&SevenZip;", sh.RegRead("HKLM\Software\7-Zip\Path"))
	path = fso.GetParentFolderName(location.pathname) & "_" & platform & ".bat"
	fso.CreateTextFile(path, True).Write content
	path = fso.GetParentFolderName(location.pathname) & "\WinMerge2011.nsi"
	parts = Split(fso.OpenTextFile(path).ReadAll, """", 3)
	parts(1) = version
	fso.CreateTextFile(path, True).Write Join(parts, """")
	close
End Sub

Sub Job_Default
	Dim element
	For Each element In shortcuts.getElementsByTagName("INPUT")
		element.checked = LinkExists(element.id)
	Next
	readme.frameElement.src = "Docs/ReleaseNotes.html"
End Sub

Sub Readme_OnLoad
	Dim href, element
	href = readme.document.location
	For Each element In sheet.getElementsByTagName("A")
		If element.href = href Then
			element.className = "selected"
		Else
			element.className = ""
		End If
	Next
End Sub

Sub PbFolder_OnClick
	Dim folder
	Set folder = sha.BrowseForFolder(0, "Install into folder:", 0, 17)
	If Not folder Is Nothing Then EdFolder.value = folder.Items.Item.path
End Sub

Sub PortableMedia_OnClick
	Dim element
	For Each element In shortcuts.getElementsByTagName("INPUT")
		element.disabled = PortableMedia.checked
	Next
End Sub

</script>
<body nowrap>
<div id='north'>
<table id='license'>
<caption>Terms of use and distribution:</caption>
<tr>
<td>
<textarea readonly>
Copyright &#169; 1996-2011 Dean Grimm / Thingamahoochie Software
Copyright &#169; 2002-2017 WinMerge Team

WinMerge 2011 is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the rightmost tab below for more details.
</textarea>
</td>
</tr>
</table>
<table id='shortcuts'>
<caption>Create shortcuts in the following places:</caption>
<tr>
<td><input id='Desktop' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='Desktop'>Desktop</label></td>
<td><input id='AllUsersDesktop' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='AllUsersDesktop'>All Users Desktop</label></td>
</tr>
<tr>
<td><input id='StartMenu' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='StartMenu'>Start Menu</label></td>
<td><input id='AllUsersStartMenu' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='AllUsersStartMenu'>All Users Start Menu</label></td>
</tr>
<tr>
<td><input id='Programs' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='Programs'>Programs</label></td>
<td><input id='AllUsersPrograms' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='AllUsersPrograms'>All Users Programs</label></td>
</tr>
<tr>
<td><input id='QuickLaunch' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='QuickLaunch'>Quick Launch</label></td>
<td><input id='ContextMenu' type='checkbox' onchange='vbs:Me.defaultChecked=True'/><label for='ContextMenu'>Shell Context Menu</label></td>
</tr>
</table>
</div>
<div id='sheet'>
<a hidefocus href='Docs/ReleaseNotes.html' target='readme'>Release Notes</a>
<a hidefocus href='Docs/ReadMe.txt' target='readme'>Readme</a>
<a hidefocus href='Docs/CHANGES' target='readme'>Change Log</a>
<a hidefocus href='Docs/COPYING' target='readme'>License Details</a>
<iframe id='readme' name='readme'></iframe>
</div>
<div id='south'>
<label id='StFolder'>Install to folder:</label><input id='EdFolder'/>
<button id='PbFolder' accesskey='B'><u>B</u>rowse</button>
<span id='media'>
<input id='PortableMedia' type='checkbox'/><label for='PortableMedia'>Portable Media</label>
</span>
<button id='PbOK' accesskey='O'><u>O</u>K</button>
<button id='PbCancel' accesskey='C'><u>C</u>ancel</button>
</div>
</body>
